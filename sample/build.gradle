//单独工程和lib的声明不同
if (isSampleRunAsApp.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'com.canzhang.bury_point'
fqlAnalytics {
    isShowDebugInfo = true
    //需要排除的包
    excludePackageList = ['com/lexinfintech/component/auto_view_point',
                          'com/lexinfintech/component/debugdialog',
                          'android/support',
                          'com/mob',
                          'cn/sharesdk',
                          'com/taobao']
    //排除包中  需要定向插桩的包（仅针对属于needExcludePackageList 中部分需要插桩的子包，方便配置，例如 'android/support'大部分不需要埋点，而其中的TabLayout需要特殊埋点需求）
    needModifyPackageList = ['android/support/design/widget/TabLayout']
}

apply plugin: 'com.tencent.matrix-plugin'
matrix {
    trace {
        enable = true	//if you don't want to use trace canary, set false
        baseMethodMapFile = "${project.buildDir}/matrix_output/Debug.methodmap"
        blackListFile = "${project.projectDir}/matrixTrace/blackMethodList.txt"
    }
}

android {
    signingConfigs {
        config {
            keyAlias 'sample'
            keyPassword '123456'
            storeFile file('sample.jks')
            storePassword '123456'
            // 默认情况下，Android Gradle 插件 2.2 同时启用这两个签署架构。
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    resourcePrefix project.getName() + "_"
    compileSdkVersion 28
    resourcePrefix project.getName() + "_"
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        //作为单独工程，需要设置applicationID
        if (isSampleRunAsApp.toBoolean()) {
            applicationId "com.canzhang.sample"
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config
    }
    compileOptions {//flutter 需要这样声明
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {//忽略一些命名规则
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isSampleRunAsApp.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                //多增加一个资源调试路径 src/main/debug/res
                res.srcDirs = ['src/main/res', 'src/main/debug/res']

            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //module 模式下要 排除src/test/文件夹下的所有文件
                    exclude 'src/debug/'
                }
            }

            //为了让项目能够找到我们的 so 库，在 build.gradle 文件夹的 android 下添加：
            jni.srcDirs = []
            // disable automatic ndk-build call, which ignore our Android.mk(屏蔽掉默认的jni编译生成过程)
            jniLibs.srcDirs = ['src/main/libs']

        }


    }
    buildTypes {
        release {
            minifyEnabled true//开启混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:' + rootProject.ext.constraintLayoutVersion
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:' + rootProject.ext.supportVersion
    testImplementation "junit:junit:${rootProject.ext.junitVersion}"
    androidTestImplementation "com.android.support.test:runner:${rootProject.ext.supportTestRunnerVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.ext.supportTestEspressoCoreVersion}"
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:${rootProject.ext.baseRecyclerViewAdapterHelperVersion}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.supportVersion}"

    //圆角布局
    api 'com.flyco.roundview:FlycoRoundView_Lib:' + rootProject.ext.roundViewVersion
    implementation project(':debug-dialog')

    //二维码
    implementation 'com.google.zxing:core:' + rootProject.ext.zxingCoreVersion

    //weex
    implementation "com.alibaba:fastjson:${rootProject.ext.fastjsonVersion}"
    implementation "com.taobao.android:weex_sdk:${rootProject.ext.weexVersion}"

    //glide
    implementation 'com.github.bumptech.glide:glide:' + rootProject.ext.glideVersion
    annotationProcessor 'com.github.bumptech.glide:compiler:' + rootProject.ext.glideVersion

    //rxjava
    implementation "io.reactivex.rxjava2:rxandroid:${rootProject.ext.rxandroid2Version}"
    implementation "io.reactivex.rxjava2:rxjava:${rootProject.ext.rxjava2Version}"

    //阴影效果，这个会导致控件变小 https://github.com/JuHonggang/ShadowDrawable
    implementation 'com.github.JuHonggang:ShadowDrawable:0.1'

    //阴影效果 https://github.com/harjot-oberai/MaterialShadows

    //    implementation 'com.sdsmdg.harjot:materialshadows:1.2.5'

    //图片压缩
    implementation 'top.zibin:Luban:1.1.8'
    implementation project(':base')
    implementation project(':flutter')
    implementation 'com.android.support:design:' + rootProject.ext.supportVersion
    api "io.reactivex.rxjava2:rxandroid:${rootProject.ext.rxandroid2Version}"
    api "io.reactivex.rxjava2:rxjava:${rootProject.ext.rxjava2Version}"

    //第三方卡顿监控框架
    api 'com.github.markzhai:blockcanary-android:1.5.0'

    //腾讯卡顿监控
    implementation group: "com.tencent.matrix", name: "matrix-trace-canary", version: MATRIX_VERSION, changing: true
}
